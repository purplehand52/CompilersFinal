$ The following program implements Deutsch's algorithm. $
$ Needs two quantum registers and two classical registers to measure and perform analysis. $

$ Generally, we perform a targeted f(x) as possible Uf: (x, y) -> (x, y + f(x)) where + represents XOR. $
$ We assume f(0) = 1, f(1) = 0. $
$ Thus the gate Uf could be represented by an inverted X gate. $

\begin_init

    $ Initialization $
    #registers quantum = 2
    #registers classical = 2
    #iters = 1000

    #set quantum states -> {(1,0),(0,0)},{(0,0),(1,0)}

    $ Block Definition $
    block U_f (x, y)
    [
        $ Reverse first register $
        X -> x 

        $ Apply controlled reverse $
        X : x -> y 

        $ Revert to original $
        X -> x 
    ]

    $ Hadamard Transform $
    block had_transform (x, y)
    [
        H -> x 
        H -> y 
    ]

\end_init


\begin_main

    $ Part 1 $
    had_transform (0, 1)

    $ Part 2 $
    U_f (0, 1)

    $ Part 3 $
    H -> 0

    $ Measure $
    measure : 0 -> 0
    measure : 1 -> 1

\end_main


\begin_output

    $ Save Output $
    \save "exp_result.csv"

    $ Classical output to be condensed $
    list[uint] univ_f = condense(c_output, 1)

    $ Report univ_f $
    float conf = 0.9
    condition(univ_f[0] > conf*1000)
    {
        echo("The unknown function is a constant function.")
    }
    otherwise(univ_f[1] > conf*1000)
    {
        echo("The unknown function is not a constant function.")
    }
    otherwise
    {
        echo("The experiment has not reached conclusive results.")
    }

\end_output